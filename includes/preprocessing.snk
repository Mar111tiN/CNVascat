rule query_normal:
    input: 
        bam=get_bam_path
    output: 
        pos="chrompos/{sample}_{normal}.{chrom}.pos"
    conda:
        "../env/bcf-env.yaml"
    threads:
        config['pre']['threads']['normal']
    params:
        ref = full_path('genome'),
        qual = f"-q {config['pre']['MAPQ']} -Q {config['pre']['Q']}",
        max_depth = config['pre']['max_depth'],
        regions = lambda w: f"<(cat {full_path('bed_file')} | mawk '$0 ~ /^{w.chrom}\\t/')",
        t1 = math.floor(config['pre']['threads']['normal'] / 2),  # divide the threads between the processes
        t2 = math.ceil(config['pre']['threads']['normal'] / 2) - 1  # divide the threads between the processes
    shell:
        "bcftools mpileup {params.qual} -d {params.max_depth} -f {params.ref}"
        " -R {params.regions} --annotate FORMAT/AD --threads {params.t1} {input.bam} | "
        "bcftools call -v -Ob -m --threads {params.t2} - | "
        "bcftools query --format \"%CHROM\\t%POS\\t[%AD{{0}}]\\t[%AD{{1}}]\\n\" - > {output.pos}"


rule query_tumor:
    input: 
        bam=get_bam_path,
        regions=get_normal_pos
    output: 
        pos="chrompos/{sample}_{tumor}.{chrom}.pos"
    conda:
        "../env/bcf-env.yaml"
    threads:
        config['pre']['threads']['tumor']
    params:
        ref = full_path('genome'),
        qual = f"-q {config['pre']['MAPQ']} -Q {config['pre']['Q']}",
        max_depth = config['pre']['max_depth'],
        # reduce normal regions to chr and pos
        regions = lambda _,input: f"<(cat {input.regions} | cut -f1,2)",
        tpile = config['pre']['threads']['tumor'] - 1   # divide the threads between the processes
    shell:
        "bcftools mpileup {params.qual} -d {params.max_depth} -f {params.ref} "
        "-R {params.regions} --annotate FORMAT/AD --threads {params.tpile} {input.bam} | "
        "bcftools query --format \"%CHROM\\t%POS\\t[%AD{{0}}]\\t[%AD{{1}}]\\n\" - > {output.pos}"


rule combine_query:
    input:
        expand("chrompos/{{sample}}_{{type}}.{chrom}.pos", chrom=chrom_list)
    output:
        pos = "pos/{sample}_{type}.pos.gz"
    threads:
        2
    script:
        "../scripts/combine_query.py"
